<Start> ::= <Program>
<Program> ::= { <Global> }

<Global> ::= <GlobalDecl> | <ImportStmt> | <Function>
<GlobalDecl> ::= <Location> <Name> ":" <Typ> "=" <Expr> ";"
<ImportStmt> ::= "import" <Location> <Javascript> "as" <Name> "(" <Params> ")" ":" <Typ>";"
<Function> ::= <Location> <Typ> <Name> "(" <Params> ")" ":" <Typ> <Stmt>
<Locaiton> ::= "front" | "back"

<Stmt> ::= <IfStmt> | <WhileStmt> | <Single> ";" | <Block>
<Block> ::= "{" { <Stmt> } "}"
<IfStmt> ::= "if" "(" <Expr> ")" <Stmt> ["else"  <Stmt> ]
<WhileStmt> ::= "while" "(" <Expr> ")" <Stmt>
<Single> ::= <Declare> | <Assign> | <Discard> | <ReturnStmt> | "break" | "continue"
<Declare> ::= "let" <Name> ":" <Typ> "=" <Expr>
<Assign> ::= <Name> "=" <Expr>
<Discard> ::= <Expr>
<ReturnStmt> ::= "return" <Expr>

<Expr> ::= <BoolExpr> [<EqualOp> <BoolExpr>]
<BoolExpr> ::= <AddSubExpr> {<BoolOp> <AddSubExpr>}
<AddSubExpr> ::= <MultDivExpr> {<AddSubOp> <MultDivExpr>}
<MultDivExpr> ::= <ExpExpr> {<MultDivOp> <ExpExpr>}
<ExpExpr> ::= <UnaryExpr> {<ExpOp> <UnaryExpr>}
<UnaryExpr> ::= {<UnaryOp>} <Atom>
<Atom> ::= <Call> | <List> | <Dict> | <Const> | <Name> | "(" <Expr> ")"
<Call> ::= <Name> "(" <Args> ")"
<List> ::= "[" [<Expr> {"," <Expr>}] "]"
<Dict> ::= "{" [<Expr>":"<Expr> {"," <Expr>":"<Expr>}] "}"
<EqualOp> ::= "==" | "!=" | ">" | "<" | ">=" | "<="
<BoolOp> ::= "and" | "or"
<AddSubOp> ::= "+" | "-"
<MultDivOp> ::= "*" | "/"
<ExpOp> ::= "^"
<UnaryOp> ::= "-" | "+"

<Const> ::= <String> | <Number> | <Bool>
<Bool> ::= "True" | "False"
<String> ::= regex("(:?[^\"]|\\\")*")
<Number> ::= regex("\d+(:?\.\d*)?|\.\d+")

<Args> ::= [<Expr> {"," <Expr>}]
<Params> ::= [<Typ> <Name> {"," <Typ> <Name>}]
<Name> ::= regex("[\w_][\w_\d]*")
<Typ> ::= "String" | "Number" | "List" | "Dictionary" | "Boolean" | "Void"

<JavaScript> ::= <String>

<Start> ::= <Program>
<Program> ::= { <Global> }

<Global> ::= <GlobalDecl> | <Import> | <Function>
<GlobalDecl> ::= <Location> <Type> <Name> "=" <Const> ";"
<Import> ::= "import" <Location> <Javascript> "as" <Name> "(" <Params> ")" ";"
<Function> ::= <Location> <Type> <Name> "(" <Params> ")" <Statement>

<Statement> ::= <If> | <While> | <Single> ";" | <Statements>
<Statements> ::= "{" { <Statement> } "}"
<If> ::= "if" "(" <Expr> ")" <Statement> ["else"  <Statement> ]
<While> ::= "while" "(" <Expr> ")" <Statement>
<Single> ::= <Declaration> | <Assignment> | <Discard> | <Return> | "break" | "continue"

<Declaration> ::= <Type> <Name> "=" <Expr>
<Assignment> ::= <Name> "=" <Expr>
<Discard> ::= <Expr>
<Return> ::= "return" <Expr>

<Expr> ::= <BoolExpr> [<EqualOp> <BoolExpr>]
<BoolExpr> ::= <P/MExpr> {<BoolOp> <P/MExpr>}
<P/MExpr> ::= <M/DExpr> {<P/MOp> <M/DExpr>}
<M/DExpr> ::= <ExpExpr> {<M/DOp> <ExpExpr>}
<ExpExpr> ::= <UnaryExpr> {<ExpOp> <UnaryExpr>}
<UnaryExpr> ::= {<UnaryOp>} <Atom>
<Atom> ::= <Call> | <List> | <Dictionary> | <Const> | <Name> | "(" <Expr> ")"
<Call> ::= <Name> "(" <Args> ")"
<List> ::= "[" [<Expr> {"," <Expr>}] "]"
<Dictionary> ::= "{" [<Expr>":"<Expr> {"," <Expr>":"<Expr>}] "}"
<EqualOp> ::= "==" | "!=" | ">" | "<" | ">=" | "<="
<BoolOp> ::= "and" | "or"
<P/MOp> ::= "+" | "-"
<M/DOp> ::= "*" | "/"
<ExpOp> ::= "^"
<UnaryOp> ::= "-" | "+"

<Const> ::= <String> | <Number> | <Bool>
<Bool> ::= "True" | "False"
<String> ::= regex("(:?[^\"]|\\\")*")
<Number> ::= regex("\d+(:?\.\d*)?|\.\d+")

<Args> ::= [<Expr> {"," <Expr>}]
<Params> ::= [<Type> <Name> {"," <Type> <Name>}]
<Name> ::= regex("[\w_][\w_\d]*")
<Type> ::= "string" | "num" | "list" | "struct" | "void"

<JavaScript> ::= <String>
